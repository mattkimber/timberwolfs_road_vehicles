VEHICLESPRITES(foden_5t, 48)

SPRITEGROUPS(foden_5t)
SPRITEGROUPS_PIECE_GOODS(foden_5t)
SPRITEGROUPS_LIVESTOCK(foden_5t)
SPRITEGROUPS_TANKER(foden_5t)

/* Define the road vehicle */
item(FEAT_ROADVEHS, foden_5t, 4021) {
  property {
      name:                           string(STR_NAME_FODEN_5T);
      climates_available:             param_enable_steam_freight ? bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL) : NO_CLIMATE;
      introduction_date:              date(1902,01,01);
      model_life:                     20;
      /* retire_early not set, use default retirement behaviour */
      vehicle_life:                   15;
      reliability_decay:              20;
      refittable_cargo_classes:       bitmask(CC_BULK, CC_LIQUID, CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED, CC_ARMOURED, CC_MAIL);
      non_refittable_cargo_classes:   bitmask(CC_PASSENGERS);
      default_cargo_type:             GOOD;
      loading_speed:                  5;
      cost_factor:                    57;
      running_cost_factor:            param_realistic_early_vehicle_capacity ? 25 : 51;
      /* cargo_age_period is left at default */

      /* RV-specific properties */
      sprite_id:                      SPRITE_ID_NEW_ROADVEH; //enable new graphics
      speed:                          18 mph;
      misc_flags:                     bitmask(ROADVEH_FLAG_2CC);
      refit_cost:                     0; // Refitting is free
      /* callback_flags are not set, no need to manually enable callbacks */
      running_cost_base:              RUNNING_COST_ROADVEH;
      power:                          35 hp;
      weight:                         5 ton;
      /* TE and air drag coefficient is left at default */
      cargo_capacity:                 param_realistic_early_vehicle_capacity ? 5 : 16;
      sound_effect:                   SOUND_TRAIN;
      visual_effect:                  visual_effect(VISUAL_EFFECT_STEAM, -4);
  }
  graphics {
    CUSTOMGRAPHICSBLOCK(foden_5t, param_realistic_early_vehicle_capacity ? 5 : 16, group, group, group)
    additional_text: return string(STR_USAGE_STEAM);
  }
}
