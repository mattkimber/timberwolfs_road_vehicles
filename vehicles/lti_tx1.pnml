VEHICLESPRITES_BUS(lti_tx1, 32)
SPRITES2X_BUS(lti_tx1, 32)

VEHICLESPRITES_BUS(lti_tx1_black, 32)
SPRITES2X_BUS(lti_tx1_black, 32)

switch(FEAT_ROADVEHS, SELF, switch_lti_tx1_graphics, cargo_subtype) {
  0: return spriteset_lti_tx1;
  1: return spriteset_lti_tx1_black;
  return spriteset_lti_tx1;
}

/* Define the road vehicle */
item(FEAT_ROADVEHS, lti_tx1, 10971) {
  property {
      name:                           string(STR_NAME_LTI_TX1);
      climates_available:             param_enable_cars ? bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL) : NO_CLIMATE;
      introduction_date:              date(1997,01,01);
      model_life:                     -1;
      /* retire_early not set, use default retirement behaviour */
      vehicle_life:                   15;
      reliability_decay:              12;
      refittable_cargo_classes:       bitmask(CC_PASSENGERS);
      non_refittable_cargo_classes:   bitmask(CC_BULK, CC_LIQUID, CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED, CC_ARMOURED, CC_MAIL);
      default_cargo_type:             PASS;
      loading_speed:                  5;
      cost_factor:                    67;
      running_cost_factor:            60;
      
      /* Cargo ages slower in cabs to simulate greater comfort (default 185) */
      cargo_age_period:               230;

      /* RV-specific properties */
      sprite_id:                      SPRITE_ID_NEW_ROADVEH; //enable new graphics
      speed:                          81 mph;
      misc_flags:                     bitmask(ROADVEH_FLAG_2CC);
      refit_cost:                     0; // Refitting is free
      /* callback_flags are not set, no need to manually enable callbacks */
      running_cost_base:              RUNNING_COST_ROADVEH;
      power:                          88 hp;
      weight:                         1.8 ton;
      /* TE and air drag coefficient is left at default */
      cargo_capacity:                 5;
      sound_effect:                   SOUND_BUS_START_PULL_AWAY;
      /* Visual effect is left at default (no effect) */
  }

  graphics {
    cargo_subtype_text: switch_cab_livery;
    default: switch_lti_tx1_graphics;
  }
}
