spriteset (spriteset_ford_transit_mk3, "png/ford_transit_mk3.vox.png") {
  template_length_32()
}

spritegroup spritegroup_ford_transit_mk3 {
	loading: spriteset_ford_transit_mk3;
	loaded:  spriteset_ford_transit_mk3;
}

alternative_sprites(spriteset_ford_transit_mk3, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "2x/ford_transit_mk3.vox.png") { template_length_32_2x() }
alternative_sprites(spriteset_ford_transit_mk3, ZOOM_LEVEL_IN_2X, BIT_DEPTH_32BPP, "2x_32bpp/ford_transit_mk3.vox.png", "2x_32bpp/ford_transit_mk3.vox.mask.png") { template_length_32_2x() }
alternative_sprites(spriteset_ford_transit_mk3, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "32bpp/ford_transit_mk3.vox.png", "32bpp/ford_transit_mk3.vox.mask.png") { template_length_32() }

/* Define the road vehicle */
item(FEAT_ROADVEHS, ford_transit_mk3, 7861) {
  property {
      name:                           string(STR_NAME_FORD_TRANSIT_MK3);
      climates_available:             param_enable_vans ? bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL) : NO_CLIMATE;
      introduction_date:              date(1986,01,01);
      model_life:                     20;
      /* retire_early not set, use default retirement behaviour */
      vehicle_life:                   14;
      reliability_decay:              20;
      refittable_cargo_classes:       bitmask(CC_EXPRESS, CC_MAIL, CC_REFRIGERATED, CC_ARMOURED);
      non_refittable_cargo_classes:   bitmask(CC_PASSENGERS, CC_BULK);
      default_cargo_type:             GOOD;
      loading_speed:                  5;
      cost_factor:                    94;
      running_cost_factor:            param_realistic_modern_vehicle_capacity ? 35 : 85;
      /* cargo_age_period is left at default */

      /* RV-specific properties */
      sprite_id:                      SPRITE_ID_NEW_ROADVEH; //enable new graphics
      speed:                          80 mph;
      misc_flags:                     bitmask(ROADVEH_FLAG_2CC);
      refit_cost:                     0; // Refitting is free
      /* callback_flags are not set, no need to manually enable callbacks */
      running_cost_base:              RUNNING_COST_ROADVEH;
      power:                          100 hp;
      weight:                         1.5 ton;
      /* TE and air drag coefficient is left at default */
      cargo_capacity:                 param_realistic_modern_vehicle_capacity ? 3 : 10;
      sound_effect:                   SOUND_BUS_START_PULL_AWAY;
      /* Visual effect is left at default (no effect) */
  }

  graphics {
    default: spritegroup_ford_transit_mk3;
    cargo_capacity: param_realistic_modern_vehicle_capacity ? 3 : 10;
    speed: param_car_speed_limits ? 56 : 64;
  }
}
