VEHICLESPRITES_GOODS_MAIL(scammell_scarab_trailer, 32)
SPRITES2X_GOODS_MAIL(scammell_scarab_trailer, 32)
SPRITEGROUPS_PIECE_GOODS(scammell_scarab_trailer)
SPRITEGROUPS_MAIL(scammell_scarab_trailer)
VEHICLESPRITES_BUS(scammell_scarab, 16)
SPRITES2X_BUS(scammell_scarab, 16)
PURCHASE_SPRITESET(scammell_scarab)

switch (FEAT_ROADVEHS, SELF, switch_scammell_scarab_articulation, extra_callback_info1) {
  0..1: return scammell_scarab;
  return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_ROADVEHS, SELF, switch_scammell_scarab_graphics_goods, position_in_consist ) {
  0: spriteset_scammell_scarab;
  spritegroup_scammell_scarab_trailer_goods;
}

switch (FEAT_ROADVEHS, SELF, switch_scammell_scarab_graphics_mail, position_in_consist ) {
  0: spriteset_scammell_scarab;
  spritegroup_scammell_scarab_trailer_mail;
}

switch (FEAT_ROADVEHS, SELF, switch_scammell_scarab_capacity, position_in_consist ) {
  0: return 1;
  return 5;
}

switch (FEAT_ROADVEHS, SELF, switch_scammell_scarab_length, position_in_consist ) {
  0: return 2;
  return 4;
}


/* Define the road vehicle */
item(FEAT_ROADVEHS, scammell_scarab, 4481) {
  property {
      name:                           string(STR_SCAMMELL_SCARAB);
      climates_available:             param_enable_lorries ? bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL) : NO_CLIMATE;
      introduction_date:              date(1948,01,01);
      model_life:                     20;
      /* retire_early not set, use default retirement behaviour */
      vehicle_life:                   25;
      reliability_decay:              20;
      refittable_cargo_classes:       bitmask(CC_EXPRESS, CC_MAIL);
      non_refittable_cargo_classes:   bitmask(CC_PASSENGERS, CC_REFRIGERATED, CC_BULK);
      default_cargo_type:             MAIL;
      loading_speed:                  5;
      cost_factor:                    54;
      running_cost_factor:            48;
      /* cargo_age_period is left at default */

      /* RV-specific properties */
      sprite_id:                      SPRITE_ID_NEW_ROADVEH; //enable new graphics
      speed:                          43 mph;
      misc_flags:                     bitmask(ROADVEH_FLAG_2CC);
      refit_cost:                     0; // Refitting is free
      /* callback_flags are not set, no need to manually enable callbacks */
      running_cost_base:              RUNNING_COST_ROADVEH;
      power:                          35 hp;
      weight:                         1.5 ton;
      /* TE and air drag coefficient is left at default */
      cargo_capacity:                 3;
      sound_effect:                   SOUND_BUS_START_PULL_AWAY;
      /* Visual effect is left at default (no effect) */
  }

  graphics {
      articulated_part: switch_scammell_scarab_articulation;
      cargo_capacity: switch_scammell_scarab_capacity;
      purchase_cargo_capacity: spriteset_scammell_scarab;
      length: switch_scammell_scarab_length; 
      purchase: spriteset_purchase_scammell_scarab;
      MAIL: switch_scammell_scarab_graphics_mail;
      default: switch_scammell_scarab_graphics_goods;
  }
}
