ARTIC(renault_magnum, modern_trailer, 16, 50, 30)

/* Define the road vehicle */
item(FEAT_ROADVEHS, vehicle_renault_magnum, 5011) {
  property {
      name:                           string(STR_NAME_RENAULT_MAGNUM);
      climates_available:             param_enable_lorries ? bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL) : NO_CLIMATE;
      introduction_date:              date(2001,01,01);
      model_life:                     -1;
      /* retire_early not set, use default retirement behaviour */
      vehicle_life:                   14;
      reliability_decay:              20;
      refittable_cargo_classes:       bitmask(CC_BULK, CC_LIQUID, CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED, CC_ARMOURED, CC_MAIL);
      non_refittable_cargo_classes:   bitmask(CC_PASSENGERS);
      default_cargo_type:             GOOD;
      loading_speed:                  5;
      cost_factor:                    192;
      running_cost_factor:            170;
      /* cargo_age_period is left at default */

      /* RV-specific properties */
      sprite_id:                      SPRITE_ID_NEW_ROADVEH; //enable new graphics
      speed:                          70 mph;
      misc_flags:                     bitmask(ROADVEH_FLAG_2CC);
      refit_cost:                     0; // Refitting is free
      /* callback_flags are not set, no need to manually enable callbacks */
      running_cost_base:              RUNNING_COST_ROADVEH;
      power:                          440 hp;
      weight:                         12.5 ton;
      /* TE and air drag coefficient is left at default */
      cargo_capacity:                 param_realistic_modern_vehicle_capacity ? 15 : 25;
      sound_effect:                   SOUND_BUS_START_PULL_AWAY;
      /* Visual effect is left at default (no effect) */
  }

  graphics {
    speed: (build_year >= 1991 && param_speed_limiters) ? 45 : 56;
    ARTIC_GRAPHICS_BLOCK(renault_magnum)
    additional_text: return string(STR_USAGE_ARTICULATED_HGV);
  }
}
