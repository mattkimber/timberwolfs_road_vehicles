VEHICLESPRITES_RAIL(dlr_b07_front, 56)
VEHICLESPRITES_RAIL(dlr_b07_rear, 56)

switch (FEAT_TRAINS, SELF, switch_dlr_b07_rail_articulation, extra_callback_info1) {
  0..5: return dlr_b07_rail;
  return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_dlr_b07_rail_graphics, position_in_consist ) {
  0: spriteset_dlr_b07_front_rail;
  1: spriteset_dlr_b07_rear_rail;
  2: spriteset_dlr_b07_front_rail;
  3: spriteset_dlr_b07_rear_rail;
  4: spriteset_dlr_b07_front_rail;
  spriteset_dlr_b07_rear_rail;
}


/* Define the road vehicle */
item(FEAT_TRAINS, dlr_b07_rail, 10082) {
  property {
      name:                           string(STR_NAME_DLR_B07);
      climates_available:             param_enable_passenger_trams && (param_lr_mode == 2 || param_lr_mode == 3) ? bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL) : NO_CLIMATE;
      introduction_date:              date(2008,01,01);
      model_life:                     -1;
      /* retire_early not set, use default retirement behaviour */
      vehicle_life:                   25;
      reliability_decay:              20;
      refittable_cargo_classes:       bitmask(CC_PASSENGERS);
      non_refittable_cargo_classes:   bitmask(CC_BULK, CC_LIQUID, CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED, CC_ARMOURED, CC_MAIL);
      default_cargo_type:             PASS;
      loading_speed:                  14;
      /* Rail cost: (*14/25), running cost (/3) */
      cost_factor:                    91;
      running_cost_factor:            64;
      /* cargo_age_period is left at default */


      /* Rail-specific properties */
      sprite_id:                      SPRITE_ID_NEW_TRAIN; //enable new graphics
      speed:                          62 mph;
      misc_flags:                     bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU);
      refit_cost:                     0; // Refitting is free
      /* callback_flags are not set, no need to manually enable callbacks */
      running_cost_base:              RUNNING_COST_ELECTRIC;
      power:                          375 hp;
      weight:                         36 ton;
      engine_class:                   ENGINE_CLASS_ELECTRIC;
      tractive_effort_coefficient:    0.3;
      /* TE and air drag coefficient is left at default */
      cargo_capacity:                 76;
  }

  graphics {
    additional_text: return string(STR_USAGE_DLR_B07);
    articulated_part: switch_dlr_b07_rail_articulation;
    default: switch_dlr_b07_rail_graphics;
    cargo_capacity: return 76;
    purchase_cargo_capacity: return 76;
    length: return 7;
    purchase: spriteset_purchase_dlr_b07;
  }
}
