VEHICLESPRITES_TRAM(flexity_2_front, 48)
VEHICLESPRITES_TRAM(flexity_2_rear, 48)
VEHICLESPRITES_TRAM(flexity_2_mid_1, 40)
VEHICLESPRITES_TRAM(flexity_2_mid_2, 32)
PURCHASE_SPRITESET(flexity_2)

switch (FEAT_ROADVEHS, SELF, switch_flexity_2_articulation, extra_callback_info1) {
  0..4: return flexity_2;
  return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_ROADVEHS, SELF, switch_flexity_2_graphics, position_in_consist ) {
  0: spriteset_flexity_2_front;
  1: spriteset_flexity_2_mid_1;
  2: spriteset_flexity_2_mid_2;
  3: spriteset_flexity_2_mid_1;
  spriteset_flexity_2_rear;
}

switch (FEAT_ROADVEHS, SELF, switch_flexity_2_capacity, position_in_consist ) {
  0: return 44;
  1: return 50;
  2: return 32;
  3: return 50;
  return 44;
}

switch (FEAT_ROADVEHS, SELF, switch_flexity_2_length, position_in_consist ) {
  0: return 6;
  1: return 5;
  2: return 4;
  3: return 5;
  return 6;
}



/* Define the road vehicle */
item(FEAT_ROADVEHS, flexity_2, 14101) {
  property {
      name:                           string(STR_NAME_FLEXITY_2);
      climates_available:             param_enable_passenger_trams ? bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL) : NO_CLIMATE;
      introduction_date:              date(2010,11,01);
      model_life:                     -1;
      /* retire_early not set, use default retirement behaviour */
      vehicle_life:                   25;
      reliability_decay:              20;
      refittable_cargo_classes:       bitmask(CC_PASSENGERS);
      non_refittable_cargo_classes:   bitmask(CC_BULK, CC_LIQUID, CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED, CC_ARMOURED, CC_MAIL);
      default_cargo_type:             PASS;
      loading_speed:                  12;
      cost_factor:                    122;
      running_cost_factor:            153;
      /* cargo_age_period is left at default */

      /* RV-specific properties */
      sprite_id:                      SPRITE_ID_NEW_ROADVEH; //enable new graphics
      speed:                          44 mph;
      misc_flags:                     bitmask(ROADVEH_FLAG_TRAM, ROADVEH_FLAG_2CC);
      refit_cost:                     0; // Refitting is free
      /* callback_flags are not set, no need to manually enable callbacks */
      running_cost_base:              RUNNING_COST_ROADVEH;
      power:                          640 hp;
      weight:                         41 ton;
      /* TE and air drag coefficient is left at default */
      cargo_capacity:                 44;

      sound_effect:                   SOUND_ELECTRIC_SPARK;
  }

  graphics {
    additional_text: return string(STR_USAGE_FLEXITY_2);
    articulated_part: switch_flexity_2_articulation;
    default: switch_flexity_2_graphics;
    cargo_capacity: switch_flexity_2_capacity;
    purchase_cargo_capacity: return 44;
    length: switch_flexity_2_length;
    purchase: spriteset_purchase_flexity_2; 
  }
}
